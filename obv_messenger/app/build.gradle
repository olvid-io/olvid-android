import org.gradle.internal.os.OperatingSystem
OperatingSystem os = OperatingSystem.current()

apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdk 34

    defaultConfig {
        manifestPlaceholders
        applicationId "io.olvid.messenger"
        minSdkVersion 21
        targetSdk 34
        versionCode 242
        versionName "2.2.1"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
        resourceConfigurations += ['en', 'fr']

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental": "true"
                ]
            }
        }
        manifestPlaceholders.appAuthRedirectScheme = "olvid.openid"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        /////////////
        // Switch this settings to "false" to disable SSL handshake verification altogether.
        // - when "true", Olvid will pin all SSL certificates it encounters so that it may warn the user when a certificate changes
        // - in some contexts (typically with an SSL proxy configured at the OS level) this handshake verification may cause issues
        // If unsure, leave this "true".
        buildConfigField "boolean", "ENABLE_SSL_HANDSHAKE_VERIFICATION", "true"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility 11
        targetCompatibility 17
    }
    kotlin {
        jvmToolchain(17)
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.4"
    }

    flavorDimensions += "features"
    flavorDimensions += "google-services"

    productFlavors {
        prod {
            dimension "features"
            buildConfigField "String", "HARDCODED_API_KEY", "null"
            buildConfigField "String", "HARDCODED_DATABASE_SECRET", "null"
            buildConfigField "String", "CONTENT_PROVIDER_URI_PREFIX", "\"content://io.olvid.messenger.PROVIDER/\""
            buildConfigField "String", "SERVER_NAME", "\"https://server.olvid.io\""
            buildConfigField "String", "KEYCLOAK_REDIRECT_URL", "\"https://openid-redirect.olvid.io/\""
            buildConfigField "int", "LOG_LEVEL", "io.olvid.engine.Logger.INFO"
            manifestPlaceholders = [
                    KEYCLOAK_REDIRECT_HOST: "openid-redirect.olvid.io",
                    MAPS_API_KEY: "<please fill in your own API key>"
            ]
        }

        full {
            dimension "google-services"

            buildConfigField "boolean", "USE_BILLING_LIB", "true"
            buildConfigField "boolean", "USE_FIREBASE_LIB", "true"
            buildConfigField "boolean", "USE_GOOGLE_LIBS", "true"
        }

        nogoogle {
            dimension "google-services"
            applicationIdSuffix '.nogoogle'
            versionNameSuffix '-nogoogle'

            buildConfigField "boolean", "USE_BILLING_LIB", "false"
            buildConfigField "boolean", "USE_FIREBASE_LIB", "false"
            buildConfigField "boolean", "USE_GOOGLE_LIBS", "false"
        }
    }

    namespace 'io.olvid.messenger'
    kotlinOptions {
        jvmTarget = '17'
    }
}


dependencies {
    testImplementation "junit:junit:4.13.2"
    annotationProcessor 'androidx.room:room-compiler:2.6.1'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation project(path: ':libwebrtc')
    implementation project(path: ':sardine-android')
    implementation project(':engine')


    implementation 'com.google.android.material:material:1.12.0'

    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.core:core-ktx:1.13.1'

    def composeBom = platform 'androidx.compose:compose-bom:2023.10.01'
    implementation composeBom
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.animation:animation-graphics'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    final def lifecycle_version = "2.7.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    debugImplementation 'androidx.compose.ui:ui-tooling'
    final def accompanist_version = "0.32.0"
    implementation "com.google.accompanist:accompanist-themeadapter-appcompat:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation 'sh.calvin.reorderable:reorderable:1.5.0'

    final def coil_version = "2.6.0"
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-video:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"

    // starting with zxing 3.4.0, API level 24 is required...
    implementation 'com.google.zxing:core:3.3.3'

    // starting with jackson 2.14, API level 26 is required...
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.emoji2:emoji2:1.4.0'
    implementation 'androidx.emoji2:emoji2-bundled:1.5.0-alpha01'
    implementation 'androidx.activity:activity:1.9.0'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.camera:camera-camera2:1.3.3'
    implementation 'androidx.camera:camera-lifecycle:1.3.3'
    implementation 'androidx.camera:camera-view:1.3.3'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation 'androidx.fragment:fragment:1.7.1'
    implementation 'androidx.media3:media3-exoplayer:1.3.1'
    implementation 'androidx.media3:media3-ui:1.3.1'
    implementation 'androidx.media:media:1.7.0'
    final def navigation_version = "2.7.7"
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-ui:$navigation_version"
    implementation "androidx.navigation:navigation-compose:$navigation_version"
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'net.zetetic:sqlcipher-android:4.5.6@aar'
    implementation 'androidx.sharetarget:sharetarget:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.9.0'
    implementation 'androidx.datastore:datastore-preferences:1.1.1'
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha06'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'
    implementation 'org.jsoup:jsoup:1.17.2'

    // webclient
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'

    // openID
    implementation 'net.openid:appauth:0.11.1'
    implementation 'org.bitbucket.b_c:jose4j:0.9.6'

    // map libre integration
    implementation 'org.maplibre.gl:android-sdk:10.3.0'
    implementation 'org.maplibre.gl:android-plugin-annotation-v9:2.0.2'

    final def commonmark_version = "0.22.0"
    implementation "org.commonmark:commonmark:$commonmark_version"
    implementation "org.commonmark:commonmark-ext-gfm-strikethrough:$commonmark_version"

    ///////////
    // Google closed source libs
    ///////////

    // Billing
    fullImplementation 'com.android.billingclient:billing:7.0.0'

    // Firebase
    fullImplementation('com.google.firebase:firebase-messaging:24.0.0') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }

    // google map integration
    fullImplementation 'com.google.android.gms:play-services-maps:18.2.0'
    fullImplementation 'com.google.android.gms:play-services-location:21.3.0'

    // Google Drive
    fullImplementation 'com.google.android.gms:play-services-auth:21.2.0'
    fullImplementation('com.google.http-client:google-http-client-gson:1.44.2') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.api-client:google-api-client-android:2.6.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    fullImplementation('com.google.apis:google-api-services-drive:v3-rev20221219-2.0.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    // opensource license page
    fullImplementation 'com.google.android.gms:play-services-oss-licenses:17.1.0'

    // ML kit QR-code scanning
    fullImplementation 'com.google.mlkit:barcode-scanning:17.2.0'
}

protobuf {
    protoc {
        if (os.isMacOsX()) {
            artifact = 'com.google.protobuf:protoc:3.18.0:osx-x86_64'
        } else {
            artifact = 'com.google.protobuf:protoc:3.18.0'
        }
    }
    plugins {
        javalite {
            if (os.isMacOsX()) {
                artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0:osx-x86_64'
            } else {
                artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
            }
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    remove java
                }
                task.plugins {
                    javalite { }
                }
            }
        }
    }
}

// disable google services plugin task for nogoogle flavor
android.applicationVariants.all { variant ->
    def googleTask = tasks.findByName("process${variant.name.capitalize()}GoogleServices")
    googleTask.enabled = !variant.flavorName.contains("Nogoogle")
}